buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.2.4'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'cobertura'
apply plugin: 'idea'

group='com.farsunset.cpush'

version = '1.0'

sourceCompatibility = 1.6
targetCompatibility = 1.6

description="A Push Module"

repositories {
    mavenCentral()
}

dependencies {
//    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile (
            'io.netty:netty:3.6.5.Final',
            'com.google.android:android:4.1.1.4',
            'org.apache.commons:commons-lang3:3.1',
            'org.slf4j:slf4j-api:1.7.9',
            'ch.qos.logback:logback-core:1.1.2',
            'ch.qos.logback:logback-classic:1.1.2',
            'com.google.guava:guava:18.0'
    )
    testCompile(
            ['org.testng:testng:6.8.8'],
            ['org.uncommons:reportng:1.1.4'],
            ['org.mockito:mockito-all:1.9.5'],
            ['org.easytesting:fest-assert-core:2.0M10'],
            // workaround for a known issue with TestNG 6.x: explicitly add Guice (Gradle will fail to run tests otherwise)
            ['com.google.inject:guice:3.0']
    )
}

//test {
//    useTestNG(){
//        //指定testng配置文件
//        suites(file('src/test/resources/testng.xml'))
//    }
//}
// ensure that test resources (src/test/resources) are added to CLASSPATH;
// see http://forums.gradle.org/gradle/topics/tests_arent_executed_when_setting_the_test_runtimeclasspath and
// http://gradle.org/docs/current/dsl/org.gradle.api.tasks.testing.Test.html#org.gradle.api.tasks.testing.Test:classpath
sourceSets {
    main {
        runtimeClasspath = files(output.resourcesDir) + runtimeClasspath
    }
    test {
        runtimeClasspath = files(output.resourcesDir) + runtimeClasspath
    }
}

test {
    useTestNG() {
        suiteXmlBuilder().suite(name: 'pushcore', parallel: 'tests') {
            test (name : 'all-tests') {
                packages {
                    'package' (name: 'com.farsunset.*')
                }
            }
        }
    }
    options {
        listeners << 'org.uncommons.reportng.HTMLReporter'
        listeners << 'org.uncommons.reportng.JUnitXMLReporter'
    }
    ext.useDefaultListeners = true
    ext.workingDirectory = 'build/'
}

// https://github.com/stevesaliman/gradle-cobertura-plugin
// http://ingorichter.blogspot.ch/2013/03/using-cobertura-with-gradle.html for list of options
cobertura {
    coverageFormats = ['xml', 'html']
}

// See http://www.gradle.org/docs/current/userguide/gradle_wrapper.html
task wrapper(type: Wrapper) {
    description = 'Install Gradle wrapper'
    gradleVersion = '2.1'
}

mainClassName = "com.farsunset.cpush.BobMain"
